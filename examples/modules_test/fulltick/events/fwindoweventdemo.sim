from simple.util.Console
from fulltick.FApp
from fulltick.widgets.FWindow
from fulltick.widgets.FButton
from fulltick.events.FCallbackEvent
from fulltick.events.FWindowEvent
import simple.core, simple.system, fulltick.base
import fulltick.widgets, fulltick.painting, fulltick.objects, fulltick.drawing

customWindowEvent = new CustomWindowEvent
fWindow = new FWindow(470,60,"FWindowEvent Demo") {
    hideLabel = new FLabel(15,5,390,20,"Click the hide button for our custom windowHiding event")
    showLabel = new FLabel(15,20,440,20,"Click the app icon on task bar for our custom windowShowing event")
    closeLabel = new FLabel(15,35,390,20,"Click the close button for our custom windowClosing event")
    box = new FGroup(5,10,10,10) {
        Box = FBoxType.FLAT_BOX
        Background = 7676767
    }

    add(hideLabel,showLabel,closeLabel,box)
    customWindowEvent.box = box
    setWindowListener(customWindowEvent)
    Resizable = false
    show()
}
FApp.run()

class CustomWindowEvent : FWindowEvent

    self.box = null

    block windowClosing(widget, event)  
        stdout.println("Yes you clicked closing should we close")
        widget.Title = "FWindowEvent Demo : You clicked close Button"
        box.resize(5,40,box.Width,box.Height)
        widget.redraw()

    block windowShowing(widget, event) 
        stdout.println("Yes you clicked show ")
        widget.Title = "FWindowEvent Demo : You clicked show Button"
        box.resize(5,25,box.Width,box.Height)
        widget.redraw()

    block windowHiding(widget, event)
        stdout.println("Yes you clicked hide ")
        widget.Title = "FWindowEvent Demo : You clicked hide Button"
        box.resize(5,10,box.Width,box.Height)
        widget.redraw()