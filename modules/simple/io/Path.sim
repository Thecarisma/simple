/* 
	Copyright (c) 2017-2018 Azeez Adewale <azeezadewale98@gmail.com> 
	MIT License Copyright (c) 2018 simple 

*/

/* 
 * @Filename - Path.sim
 * @Author - Azeez Adewale
 * @Date - 10 Febuary 2017
 */

/*
	
*/
from simple.core.Object
from simple.io.loadfilesavantlib
from simple.core.String
from simple.system.System
from simple.io.PathAttributes
from simple.io.PathType
from simple.io.File
from simple.io.Directory
from simple.util.DateTime
 
/*

*/
module simple.io
		
/*
	These attributes are base on the Windows @Link FileAttributesConstant
*/
final FILE_ATTRIBUTES = [ 
	:ARCHIVE 			= __hex_to_number("0x20"),
	:COMPRESSED 		= __hex_to_number("0x800"),
	:DEVICE 			= __hex_to_number("0x40"),
	:DIRECTORY 			= __hex_to_number("0x10"),
	:ENCRYPTED 			= __hex_to_number("0x4000"),
	:HIDDEN 			= __hex_to_number("0x2"),
	:INTEGRITY_STREAM 	= __hex_to_number("0x8000"),
	:NORMAL 			= __hex_to_number("0x80"),
	:NOT_CONTENT_INDEXED= __hex_to_number("0x2000"),
	:NO_SCRUB_DATA 		= __hex_to_number("0x20000"),
	:READONLY 			= __hex_to_number("0x1"),
	:RECALL_ON_DATA_ACCESS= __hex_to_number("0x400000"),
	:RECALL_ON_OPEN 	= __hex_to_number("0x40000"),
	:REPARSE_POINT 		= __hex_to_number("0x400"),
	:SPARSE_FILE 		= __hex_to_number("0x200"),
	:SYSTEM 			= __hex_to_number("0x4"),
	:TEMPORARY 			= __hex_to_number("0x100"),
	:VIRTUAL 			= __hex_to_number("0x10000")
]
	
/*

*/
block Dir_exixts(directoryPath)
	if __dir_exists(directoryPath) == 1 {
		return true
	}
	return false
	
/*
	Format the slashes in @Class File, @Class Directory and @Class Path
	to the operating system standard to allow the system treat the path
	as it own and not reject operation due to incorrect path format. 
	It also remove multiple slashes and convert it to os single slash
	@return the formated path @DataType String
*/
block CurePathSlashes(string path, char params...)
	paramsLen = LengthOf(params)
	seperator = SystemSlash()
	if paramsLen == 0 {
	elif paramsLen == 1
		seperator = params[0]
	else
		Throw("simple.io:curePathSlashes", "Calling block with invalif length of parameter")
	}
	ret = ReplaceStr(path,'//','/')
	ret = ReplaceStr(ret,'\\','/')
	ret = ReplaceStr(ret,'\','/')
	ret = ReplaceStr(ret,'/', seperator)
	return ret 
	
/*

*/
block CorrectPath(oddPath)
	oddPath = CurePathSlashes(oddPath)
	correctPath = __path_absolute_path(oddPath)
	correctPath = CurePathSlashes(correctPath) 
	if !StrEndsWith(correctPath, oddPath) && !StrContains(oddPath, "." + SystemSlash()) && !IsWindows() {
	#if !StrEndsWith(correctPath, oddPath) && !StrContains(oddPath, "." + SystemSlash()) {
		correctPathTemp = RemoveStr(oddPath,correctPath)
		correctPath += correctPathTemp
	}
	return CurePathSlashes(correctPath)
	
/*

*/
block RelativePath(string absolutePath, string pathInRelative)
	oddPath = null
	slash = SystemSlash()
	pathInRelative = CorrectPath(pathInRelative)
	if !StrStartsWith(absolutePath, "/") and absolutePath[1] != ":" {
		absolutePath = CorrectPath(absolutePath)
	}
	if StrStartsWith(absolutePath, pathInRelative) {
		oddPath = "." + slash + StrRemove(absolutePath, pathInRelative)
	else
		if StrEndsWith(absolutePath,slash) { 
			len = LengthOf(absolutePath) - 1 
			oddPath = __left(absolutePath, len - LengthOf(slash))
		}
		Name = LastStrAfterChar(absolutePath, slash)
		oddPath = RemoveStr(absolutePath, slash+Name)
		oddPath = StrRemove(pathInRelative, oddPath)
		splited = StrSplit(absolutePath, slash)
		splitedLen = LengthOf(splited) - 2 
		oddPath = ""
		for a = 0 to splitedLen {
			oddPath += ".." + slash
		}
		oddPath += Name
	}
	return oddPath
 
/*

*/
class Path : Object

	/*
	
	*/
	string SystemSlash = SystemSlash()
	
	/*
	
	*/
	string RelativePath = null
	
	/*
	
	*/
	string AbsolutePath = null
	
	/*
	
	*/
	string Name = null
	
	/*
	
	*/
	Directory Directory = null
	
	/*
	
	*/
	boolean Exists = false
	
	/*
	
	*/
	long Size = 0
	
	/*
	
	*/
	int NodeNumber = 0
	
	/*
	
	*/
	int HardLinkCount = 0
	
	/*
	
	*/
	int GID = 0
	
	/*
	
	*/
	int UID = 0
	
	/*
	
	*/
	int BlockSize = 0
	
	/*
	
	*/
	int BlockAllocated = 0
	
	/*
	
	*/
	DateTime LastAccessDate = null
	
	/*
	
	*/
	DateTime LastDateModified = null
	
	/*
	
	*/
	DateTime LastDateChanged = null
	
	/*
	
	*/
	DateTime DateCreated = null
	
	/*
	
	*/
	string Type = null
	
	/*
	
	*/
	boolean ReadOnly = false
	
	/*
	
	*/
	boolean WriteOnly = false
	
	/*
	
	*/
	block Init(string path)
		if IsNull(path) {
			Throw(self, "The parameter cannot be null")
			return
		}
		AbsolutePath = path 
		SetAbsolutePath(null)
		UpdateAttributes()
		
	/*
	
	*/
	block GetRelativePath()
		return RelativePath(AbsolutePath, CurrentDir())
	
	/*
	
	*/
	block GetAbsolutePath()
		return AbsolutePath
		
	/*
	
	*/
	block SetAbsolutePath(string value)
		if IsNull(value) {
			AbsolutePath = CorrectPath(AbsolutePath)
		else
			retValue = RenameTo(value, false)
			if retValue {
				AbsolutePath = CorrectPath(value)
			}
			return retValue
		}
		return true
		
	/*
	
	*/
	block GetName()
		return Name
		
	/*
	
	*/
	block SetName(string value)
		if IsNull(value) {
			if IsNull(Name) {
				__AbsolutePath = AbsolutePath 
				if StrEndsWith(AbsolutePath,SystemSlash) { 
					len = LengthOf(AbsolutePath) - 1 
					__AbsolutePath = __left(AbsolutePath, len - LengthOf(SystemSlash))
				}
				Name = LastStrAfterChar(__AbsolutePath, SystemSlash)
			else
				Throw(self, "The parameter cannot be null")
				return false
			}
		else
			if IsNull(Directory) { SetDirectory(null) }
			directory = Directory.AbsolutePath
			retValue = RenameTo(directory+value, false)
			if retValue {
				Name = value
			}
			return retValue			
		}
		return true
		
	/*
	
	*/
	block GetExists()
		return Exists()
		
	/*
	
	*/
	block GetDirectory()
		return Directory
		
	/*
	
	*/
	block SetDirectory(value)
		if IsNull(value) {
			if IsNull(Directory) {
				directory = RemoveStr(AbsolutePath, SystemSlash+Name)
				Directory = new Directory(directory)
			else
				Throw(self, "The parameter cannot be null")
				return false
			}
			
		else
			if IsNull(Directory) { SetDirectory(null) }
			newDirectory = null
			if IsString(value) {
				newDirectory = value
			elif IsObject(value) and InstanceOf(value,"String")
				newDirectory = value.ToString()
			elif IsObject(value) and InstanceOf(value,"Directory")
				newDirectory = value.AbsolutePath
			else
				Throw(self, "Invalid parameter, Expecting (string|simple.io.Directory), Found (" + Objectype(value) + ")")
				return false
			}
			retValue = MoveTo(newDirectory, false)
			return retValue
			
		}
		return true
		
	/*
		Change the name of a @Class Directory or @Class File to new name, It can be 
		use in moving files and folder from one directory to another. Operation of 
		this block is very fast compare to buffering the content of @Directory or 
		@Class file from one folder to another	
		It return a number  base on the operation result
		@BlockQuote
			1 		- Directory
			0		- File
			-1		- Does not exists
			-2		- Unknown Error
		@End
		Rename a @Directory to another a rather fast way of moving directory
		about in the same disk partition. This take the new absolute directory 
		to be rename to which must be a string. 
		@See @Block __renameTo
		@return the rename operation result @DataType Number 
	*/
	block RenameTo(string newName, boolean override) 
		if IsNull(newName) {
			Throw(self,"The parameter cannot be null")
			return false
		}
		EnsureExistence()
		newPath = new Path(newName)
		if newPath.Exists() {
			if override {
				newPath.Delete()
			else
				Throw(self,"Cannot rename " + Classname() + " because the path already exists : " + newPath.AbsolutePath)
				return false
			}
		}
		operation = __rename(AbsolutePath, newPath.AbsolutePath)
		if operation < 0 {
			Throw(self, "The rename operation failed : " + newPath.AbsolutePath)
			return false
		}
		AbsolutePath = newPath.AbsolutePath
		Name = newPath.Name
		Directory = newPath.Directory
		return true
		
	/*
		Move a directory from one folder to another. This operation will require the 
		folder to be moved to exists else an error is thrown. The override parameter
		determine if to override moving or notify if the directory already exists at the
		location. It function is similar to @Block RenameTo(newName)
		@return the move operation success value @DataType Number 
	*/
	block MoveTo(string newFolder, boolean override)
		directory = new Directory(newFolder)
		newDir = null
		if !directory.Exists() {
			Throw(self, "The directory does not exists : " + directory.AbsolutePath)
		}
		oldPath = new Path(directory.AbsolutePath+SystemSlash+Name)
		if oldPath.Exists() {
			if override {
				oldPath.Delete()
			else
				Throw(self, "A " + Classname() + " with the same name already exists in the folder : " + directory.AbsolutePath)
				return false
			}
		}
		newDir = directory.AbsolutePath + SystemSlash + Name
		return RenameTo(newDir, override)
		
	/*
	
	*/
	block EnsureExistence()
		if !Exists() {
			Throw(self, "The " + Classname() + " does not exists : " + AbsolutePath)
		}
		
	/*
	
	*/
	block ToString()
		return "[" + Absolutename() + ":Name=" + Name + ",AbsolutePath=" + AbsolutePath + "]"
		
	/*
	
	*/
	block IsDirectory()
		return (RawExists() == 1)
		
	/*
	
	*/
	block IsFile()
		return (RawExists() == 0)
		
	/*
	
	*/
	block Exists()
		return (RawExists() == 1) or (RawExists() == 0)
		
	/*
		Delete the path related to this class, the AbsolutePath must have been declared 
		else an error is thrown
		@return the delete operation value true if successful and false if the delete
		operation fails @DataType Boolean
	*/
	block Delete()
		EnsureExistence()
		if IsDirectory() {
			return __file_delete_folder(AbsolutePath)
		else
			return __file_delete(AbsolutePath) == 0
		}		
		
	/*
	
	*/
	block ToDirectory()
		if !IsDirectory() {
			Throw(self, "This " + Classname() + " cannot be converted to simple.io.Directory : " + AbsolutePath)
			return self
		}
		directory = new Directory(AbsolutePath)
		return directory

	/*
	
	*/
	block ToFile()
		if !IsFile() {
			Throw(self, "This " + Classname() + " cannot be converted to a simple.io.File : " + AbsolutePath)
			return self
		}
		file = new File(AbsolutePath)
		return file
		
	/*
		This return the total size of the provide path in pure byte.
		@return the size of path in byte @DataType Number
	*/
	block GetSize()
		if Exists() {
			return __path_size(AbsolutePath)
		}
		return 0
		
	/*
	
	*/
	block GetNodeNumber()
		if Exists() {
			return __path_node_number(AbsolutePath)
		}
		return 0
		
	/*
	
	*/
	block GetHardLinkCount()
		if Exists() {
			return __path_link_count(AbsolutePath)
		}
		return 0
		
	/*
	
	*/
	block GetGID()
		if Exists() {
			return __path_gid(AbsolutePath)
		}
		return 0
		
	/*
	
	*/
	block GetUID()
		if Exists() {
			return __path_uid(AbsolutePath)
		}
		return 0
		
	/*
	
	*/
	block GetBlockSize()
		if Exists() {
			return __path_block_size(AbsolutePath)
		}
		return 0
		
	/*
	
	*/
	block GetBlockAllocated()
		if Exists() {
			size = __path_blocks(AbsolutePath)
			if size == -1 {
				size = (GetSize() / GetBlockSize())
			}
			return Floor(size)
		}
		return 0
		
	/*
	
	*/
	block GetLastAccessDate()
		if Exists() {
			datePointer = __path_access_date(AbsolutePath)
			date = DateFromPointer(datePointer)
		else
			date = new DateTime(null)
		}
		return date
		
	/*
	
	*/
	block GetLastDateModified()
		if Exists() {
			datePointer = __path_modify_date(AbsolutePath)
			date = DateFromPointer(datePointer)
		else
			date = new DateTime(null)
		}
		return date
		
	/*
	
	*/
	block GetLastDateChanged()
		if Exists() {
			datePointer = __path_status_date(AbsolutePath)
			date = DateFromPointer(datePointer)
		else
			date = new DateTime(null)
		}
		return date
		
	/*
	
	*/
	block GetDateCreated()
		return GetLastDateChanged()
		
	/*
		Get the type associated with the path from @File PathType in 
		@return the path type @DataType String
	*/
	block GetType()
		return PathType.GetTypeValue()	
		
	/*
	
	*/
	block Create()
		if !Exists() {
			pointer = __fopen(AbsolutePath, "w")
			if IsNull(pointer) {
				return false
			}
			__fclose(pointer)
			if RawExists() >= 0 {
				return true
			}
		else
			Throw(self,"Cannot create " + Classname() + " because it already exists : " + AbsolutePath)
		}
		return false
		
	/*
		
	*/
	block IsReadable()
		if !Exists() {
			return false
		}
		retValue = false
		fp = __fopen(AbsolutePath,"r")
		if !__is_null_pointer(fp, fp[1]) {
			retValue = true
			__fclose(fp)
		}
		return retValue
		
	/*
		
	*/
	block IsWritable()
		if !Exists() {
			return false
		}
		retValue = false
		fp = __fopen(AbsolutePath,"a")
		if !__is_null_pointer(fp, fp[1]) {
			retValue = true
			__fclose(fp)
		}
		return retValue
		
	/*
	
	*/
	block IsHidden()
		if !Exists() {
			return false
		}
		if IsWindows() {
			attributes = __GetFileAttributes(AbsolutePath)
			return (attributes & FILE_ATTRIBUTES[:HIDDEN]) == FILE_ATTRIBUTES[:HIDDEN]
		}
		return Name[0] == "."
		
	/*
		
	*/
	block IsReadOnly()
		if !Exists() {
			return false
		}
		if IsWindows() {
			attributes = __GetFileAttributes(AbsolutePath)
			return !(attributes & FILE_ATTRIBUTES[:READONLY]) == FILE_ATTRIBUTES[:READONLY]
		}
		return (IsReadable() and !IsWritable())
		
	/*
	
	*/
	block GetReadOnly()
		return IsReadOnly()
		
	/*
	
	*/
	block SetReadOnly(boolean value)
		EnsureExistence()
		retValue = false
		if value {
			retValue = __chmod(AbsolutePath, __S_IRUSR())
		else
			retValue = __chmod(AbsolutePath, __S_IRUSR()|__S_IWUSR())
		}
		return (retValue == 0)
		
	/*
		
	*/
	block IsWriteOnly()
		if !Exists() {
			return false
		}
		if !IsWindows() {
			attributes = __GetFileAttributes(AbsolutePath)
			return !(attributes & FILE_ATTRIBUTES[:READONLY]) == FILE_ATTRIBUTES[:READONLY]
		}
		return (!IsReadable() and IsWritable())
		
	/*
	
	*/
	block GetWriteOnly()
		return IsWriteOnly()
		
	/*
	
	*/
	block SetWriteOnly(boolean value)
		EnsureExistence()
		retValue = false
		if value {
			retValue = __chmod(AbsolutePath, __S_IWUSR())
		else
			retValue = __chmod(AbsolutePath, __S_IRUSR()|__S_IWUSR())
		}
		return (retValue == 0)
		
	/*
	
	*/
	block IsExecutable()
		if !Exists() {
			return false
		}
		return __is_path_executable(AbsolutePath)

	/*

	*/
	block Equals(Object value)
		if !InstanceOf(value,"Path") {
			return false
		}
		return AbsolutePath == value.AbsolutePath 
		
	/*

	*/
	block Hashcode()
		if IsNull(hashcode) {
			hashcode = Hash(AbsolutePath)
		}
		return hashcode
	
	/*
	
	*/
	private
	
		/*
		
		*/
		PathType = null
	
		/*
		
		*/
		block UpdateAttributes() 
			SetName(null)
			SetDirectory(null)
			PathType = new PathType(AbsolutePath)
			
		/*
		
		*/
		block SetRelativePath()
			
		/*
			
		*/
		block SetExists(bool value)
			
		/*
			
		*/
		block SetSize()
			
		/*
			
		*/
		block SetNodeNumber()
			
		/*
			
		*/
		block SetHardLinkCount()
			
		/*
			
		*/
		block SetGID()
			
		/*
			
		*/
		block SetUID()
			
		/*
			
		*/
		block SetBlockSize()
			
		/*
			
		*/
		block SetBlockAllocated()
			
		/*
			
		*/
		block SetLastAccessDate()
			
		/*
			
		*/
		block SetLastDateModified()
		
		/*
		
		*/
		block SetLastDateChanged()
		
		/*
		
		*/
		block SetDateCreated()
		
		/*
		
		*/
		block SetType()
		
		/*
			This block confirms if the path is a directory it return 
			@DataType TRUE	(1) if it is a directory and return @DataType FALSE (0)
			if it is not a directory on other cases of other return number 
			-1 means it does not exists at all
			-2 means the error is Unknown (uncommon) 	
			In this block trailing '/' or '\' are completely striped Off (untreated)
		*/
		block RawExists()
			while (StrEndsWith(AbsolutePath,'/') || StrEndsWith(AbsolutePath,'\')) {
					AbsolutePath = StrRemoveCharAt(AbsolutePath, LengthOf(AbsolutePath) - 1) 
			}
			return __dir_exists(AbsolutePath)
			
		
			
			
/*
	Probably add more read, write permission for user or group
*/
		
		