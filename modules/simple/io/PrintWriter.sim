/* 
	Copyright (c) 2017-2018 Azeez Adewale <azeezadewale98@gmail.com> 
	MIT License Copyright (c) 2018 simple-lang

*/

/* 
	#filename - PrintWriter.sim
	#date - 07 January 2019
	#time - 08 : 23 AM
	#license - MIT License Copyright (c) 2018 simple-lang
	#author - Azeez Adewale	<azeezadewale98@gmail.com> 
 */
 
/*

*/
from simple.core.Object
from simple.io.loadfilesavantlib
 
/*

*/
module simple.io

/*

*/
class PrintWriter : Object
		
	/*
	
	*/
	boolean Closable = true

	/*
	
	*/
	block Init(FileWriter fileWriter, boolean autoFlush)
		if IsNull(fileWriter) {
			Throw(self, "The parameter cannot be null ")
			return null
		}
		self.AutoFlush = autoFlush
		self.fileWriter = fileWriter
		IsOpen = true
		
		return self
	
	/*
	
	*/
	block Write(string data)
		if IsOpen {
			fileWriter.Write(data) 
			if StrContains(data,Ascii(10)) and AutoFlush {
				fileWriter.Flush()
			}
		else
			Throw(self, "The PrintWriter has been closed")
		}
		
	/*
	
	*/
	block Print(var data)
		Write(UnEscapeString(StrValueOf(data)))
		
	/*
	
	*/
	block Println(var data...)
		dataLen = LengthOf(data)
		if dataLen == 0 {	
			PrintNewLine()
			
		elif dataLen == 1
			Write(UnEscapeString(StrValueOf(data[0])))
			PrintNewLine()
		else
			Throw(self,"Calling block with extra parameters !")
			return
		}
		
	/*
	
	*/
	block PrintNewLine()
		try {
			if IsOpen {
				fileWriter.Write(LineSeperator()) 
				fileWriter.Flush()
			else
				Throw(self, "The PrintWriter has been closed")
			}
		catch
			
		}
		
	/*
	
	*/
	block Printf(params...)
		value = StrFormat(params)
		Write(value)
		
	/*
	
	*/#maybe splice to parameter :?
	block Printfi(params...)
		value = StrFormatIndex(params)
		Write(value)	

	/*
		print with fore ground color if supported
	*/
	block Printfc(number color, params...)
		if !IsOpen {
			Throw(self, "The file has been closed ")
			return false
		}
		value = StrFormatIndex(params)
		__printwfb(fileWriter.FilePointer, color, BackgroundColor, value)
		fileWriter.Flush()

	/*
		print with back ground color if supported
	*/
	block Printbc(number color, params...)
		if !IsOpen {
			Throw(self, "The file has been closed ")
			return false
		}
		value = StrFormatIndex(params)
		__printwfb(fileWriter.FilePointer, ForegroundColor, color, value)
		fileWriter.Flush()

	/*
		print with back ground color if supported
	*/
	block Printbfc(number bgcolor, number fgcolor, params...)
		if !IsOpen {
			Throw(self, "The file has been closed ")
			return false
		}
		value = StrFormatIndex(params)
		__printwfb(fileWriter.FilePointer, fgcolor, bgcolor, value)
		fileWriter.Flush()
	
	/*
	
	*/
	block Flush()
		try {
			if fileWriter.IsOpen {
				fileWriter.Flush()
			}
		catch
			
		}
	
	/*
	
	*/
	block Close()
		if IsOpen {
			if !Closable {
				fileWriter.Flush()
			else
				try {
					fileWriter.Close()
				catch
					
				}
				IsOpen = false
				fileWriter = null
			}
		}
		
	
	/*
		This block clean up the object to avoid dangling pointers and object. 
		Invoking this block assume there is no more reference to this object 
		
		@See @Block close
		@See @Block flush
	*/
	block Done() 
		Close()

	/*

	*/
	block Operator(string op,object value)
		switch op {
			case "<<"
				Print(value)
			default 
				return super.Operator(op,value)
		}
		return self
	
	/*
	
	*/
	private
	
		/*
		
		*/
		boolean IsOpen = true
	
		/*
		
		*/
		boolean AutoFlush = false

		/*
		
		*/
		FileWriter fileWriter = null
		
		/*
		
		*/
		number BackgroundColor = 0
		
		/*
		
		*/
		number ForegroundColor = 0
		
		
		
#stop flushing every print, flush on printNewLine
		