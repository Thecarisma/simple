/* 
	Copyright (c) 2018 Azeez Adewale <azeezadewale98@gmail.com> 
	MIT License Copyright (c) 2018 simple 

*/

/* 
 * @filename - Throw.sim
 * @author - Azeez Adewale
 * @date - 18 Febuary 2018
 * @Time - 02:37 PM
 */

/*
	To prevent error in operation and at all level of a program the 
	@Block throw(errorvalue) and @Block throwOnly(errorvalue) can be use 
	to show error detail and print the stack trace to locate the error 
	position. 
		The @Block throw(errorvalue) shows error detail and exit the 
	program. This is most useful to prevent erroneous program and to catch 
	error just in time.
		The @Block throwOnly(errorvalue) behaves slightly different from 
	the @Block throw(errorvalue) block as it does not force the program to
	exit after the error detail has been displayed
	
*/

from simple.debugging.Debug
 
module simple.debugging

	__DEBUG_FINAL_ERROR_EMPTY = "The error can never be empty: forcing exit of the program"
	__DEBUG_TYPE_THROW_ONLY = 0
	__DEBUG_TYPE_THROW = 1
 
/*
	Throw an error at any point in the program with this block
	the parameter @Bold errorValue @End is the main error message
	display when the error is thrown. Calling this block ends the 
	program after the error has been displayed. The error is 
	thrown in the format below
	@Code
	
		Line 24 -> Unknown Error occured
			at throw() in file Throw.sim
			at line 40 at three() in file debugging.sim
			at line 37 at two() in file debugging.sim
			at line 34 at one() in file debugging.sim
			at line 29 at main() in file debugging.sim
			at line 21 in file debugging.sim
			
	@EndCode
	
	If intention is to show an error the @Block throwOnly(errorValue)
	should be called instead because it does not exit the program
*/
block throw(errorValues...)
	errorValuesLen = lengthOf(errorValues)
	if errorValuesLen == 1 {
		object = errorValues[0]
		errorTitle = null 
		errorValue = null 
		if !isNull(object) and isObject(object) { 
			if __object_has_method(object,"throwError") {
				object.throwError()
			elif __instance_of(object, "Error")
				errorTitle = object.Title
				errorValue = object.Message
				__throw(errorTitle+" : "+errorValue)
			else 
				__throw("Invalid Error Message. Unknown Error occured")
			}
		elif !isNull(object) and isString(object) 
			if isNull(errorValue) {
				errorValue = object
			}
			__throw(errorValue)
		else
			__throw("Invalid Error Message. Unknown Error occured")
		}		
	else
		object = errorValues[0]
		errorTitle = ""
		if !isNull(object) and isObject(object) {
			errorTitle = __class_module_name(object)+"."+__class_name(object)
			errorValue = errorValues[1] 
			if isNull(errorValue) {
				errorValue = "Unknown Error occured"
			}
			__throw(errorTitle + " : " + errorValue)
		elif isString(object) 
			errorTitle = object 
			errorValue = errorValues[1]
			if isNull(errorValue) {
				if !isNull(object) {
					errorValue = object
				else
					errorValue = "Unknown Error occured"
				}
			}
			__throw(errorTitle + " : " + errorValue)
		else
			__throw("Invalid Error Message. Unknown Error occured")
		}
	}
	if isLinux() {
		@nl
	}
	#__exit__ 
	__exit(0)
	
/*
	Want to make the program error tolerant then this block is 
	the right block to from instead of the @Block throw because it does
	not exit the program after the error has been thrown. It definitely 
	far from safe as the reason why it is thrown is defied as the error 
	will be carried along to another throwOnly() statement because the
	error will still be active. so to prevent a very erroneous program 
	the @Block throw(errorvalue) should be called instead
*/
block throwOnly(errorValues...)
	errorValuesLen = lengthOf(errorValues)
	if errorValuesLen == 1 {
		object = errorValues[0] 
		errorTitle = null 
		errorValue = null
		if !isNull(object) and isObject(object) {
			if __object_has_method(object,"throwError") {
				object.throwError()
			elif __instance_of(object, "Error")
				errorTitle = object.Title
				errorValue = object.Message
				__throw(errorTitle+" : "+errorValue)
			else 
				__throw("Invalid Error Message. Unknown Error occured")
			}
		elif isString(object)
			if errorValue == "" {
				if !isNull(object) {
					errorValue = object
				else
					errorValue = "Unknown Error occured"
				}
			}
			__throw(errorValue)
		else
			__throw("Invalid Error Message. Unknown Error occured")
		}		
	else
		object = errorValues[0]
		errorTitle = ""
		if !isNull(object) and isObject(object) {
			errorTitle = __class_module_name(object)+"."+__class_name(object)
			errorValue = errorValues[1] 
			if errorValue == "" {
				errorValue = "Unknown Error occured"
			}
			__throw(errorTitle + " : " + errorValue)
		elif isString(object) 
			errorTitle = object
			errorValue = errorValues[1]
			if errorValue == "" {
				if !isNull(object) {
					errorValue = object
				else
					errorValue = "Unknown Error occured"
				}
			}
			__throw(errorTitle + " : " + errorValue)
		else
			__throw("Invalid Error Message. Unknown Error occured")
		}
	}
	if isLinux() {
		@nl
	}
	
/*
	Throw an error with a custom title and error message then exit the program
*/
block throwWithTitle(object,errorvalue)
	title = ""
	if isObject(object) {
		#get title as module from reflection
		title = ""
	elif isString(object)
		title = object
	}
	__throwWithTitle(title,errorvalue,__DEBUG_TYPE_THROW)
	
/*
	Throw an error with a custom title and error message with out exiting the program
*/
block throwOnlyWithTitle(title,errorvalue)
	__throwWithTitle(title,errorvalue,__DEBUG_TYPE_THROW_ONLY)

/*
	Use a custom title for error been thrown in the program 
	the integrity of the error title and content is verified to prevent vague
	error message. This block should not be called from any point in the 
	program as it a delegate block the two block that should be called instead are
	>	throwWithTitle(title,errorvalue)
	>	throwOnlyWithTitle(title,errorvalue)
*/
block __throwWithTitle(title,errorvalue,type)
	finalErrorValue = ""
	if title == "" {
		finalErrorValue += ""
	else
		finalErrorValue += title
	}
	finalErrorValue += " : "
	if errorvalue == "" {
		finalErrorValue += "Unknown Error Occur"
	else
		finalErrorValue += errorvalue
	}
	if finalErrorValue == "" {
		throw(__FINAL_ERROR_EMPTY)
		#__exit__ 
		__exit(0)
	}
	if type == __DEBUG_TYPE_THROW_ONLY {
		throwOnly(finalErrorValue)
	else
		throw(finalErrorValue)
	}



/*
	TODO : make dedundant block into overloads using variadic parameter
*/