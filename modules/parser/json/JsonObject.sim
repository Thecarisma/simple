/*


*/

/*

*/
from simple.core.Object
from parser.json.JsonMember

/*

*/
module parser.json

/*

*/
class JsonObject : Object

	/*
	
	*/
	final /*list*/ members = []
		
	/*
	
	*/
	boolean Dynamic = false

	/*
	
	*/
	block Init(boolean params...)
		paramsLen = LengthOf(params)
		if paramsLen == 0 {
		elif paramsLen == 1
			Dynamic = params[0]
		else
			Throw(self, "Calling block with extra parameters!")
		}
		return self
		
	/*
	
	*/
	block Add(object params...)
		name = null
		value = null
		member = null
		paramsLen = LengthOf(params)
		
		if paramsLen == 1 {
			member = params[0]
			if !InstanceOf(member,"JsonMember") {
				Throw(self, "Invalid parameter, Expected (parser.json.JsonMember), Found (" + ObjectType(member) + ")")
				return
			}
		elif paramsLen == 2
			name = params[0]
			value = params[1] 
			if IsString(name) {
				member = new JsonMember(name, value)
			else
				Throw(self, "Invalid parameter, Expected (string, object), Found (" + ObjectType(name) + "," + ObjectType(value) + ")")
				return
			}
		else
			Throw(self, "Calling block with extra parameters")
			return
		}
		__add_to_list(members, member)
		if Dynamic {
			__add_attribute_to_object(self,member.Name) 
			__set_attribute_value(self,member.Name,member.Value) 
			
			/*getter = block { 
				return Get(0)	
			}
			setter = Block(value) { 
				Set(0, value)
			}
			__add_method_to_object(self, "get" + member.Name, getter)
			__add_method_to_object(self, "set" + member.Name, setter) */
		}
		
	/*
	
	*/
	block AddAll(JsonMember mems...)
		for member in mems {
			Add(member)
		}
		
	/*
	
	*/
	block Set(object index, object value)
		if IsNumber(index) {
			if InstanceOf(value,"JsonMember") {
				members[index] = value
			else
				member = new JsonMember(members[index].Name, value)
				members[index] = member
			}
		elif IsString(index)
			membersLen = LengthOf(members) - 1
			for a = 0 to membersLen {
				member = members[a]
				if ObjEquals(member.Name, index) {
					if InstanceOf(value,"JsonMember") {
						members[a] = value
					else
						member = new JsonMember(members[a].Name, value)
						members[a] = member
					}
					return
				}
			}
			Add(index, value)
		else
			Throw(self, "Invalid parameter, Expected (String|Number, object), Found (" + ObjectType(index) + "," + ObjectType(value) + ")")
		}
	
	/*
	
	*/
	block Get(object index)
		if IsNumber(index) {
			return members[index]
		elif IsString(index) 
			membersLen = LengthOf(members) - 1
			for a = 0 to membersLen {
				if ObjEquals(members[a].GetName(), index) {
					return members[a]
				}
			}
			Throw(self, StrFormat("The requested JSON member '%s' cannot be found", index) )
		else
			Throw(self, "Invalid parameter, Expected (String|Number), Found (" + ObjectType(index) + ")")
		}
		
	/*
	
	*/
	block GetValue(object index)
		return Get(index).GetValue()
		
	/*
	
	*/
	block Remove(object index)
		if IsNumber(index) {
			obj = members[index]
			__del_from_list(members, index)
			return obj
		elif IsString(index)
			membersLen = LengthOf(members) - 1
			for a = 0 to membersLen {
				member = members[a]
				if ObjEquals(member.GetName(), index) {
					__del_from_list(members, a)
					return member
				}
			}
			return null
		else
			Throw(self, "Invalid parameter, Expected (String|Number), Found (" + ObjectType(index) + ")")
			return null
		}
		
	/*
	
	*/
	block Contains(object index)
		if IsNumber(index) {
			return (LengthOf(members) >= index)
		elif IsString(index)
			membersLen = LengthOf(members) - 1
			for a = 0 to membersLen {
				member = members[a]
				if ObjEquals(member.GetName(), index) {
					return true
				}
			}
			return false
		else
			Throw(self, "Invalid parameter, Expected (String|Number), Found (" + ObjectType(index) + ")")
			return null
		}
		
	/*
	
	*/
	block IsEmpty()
		return LengthOf(members) == 0
		
	/*
	
	*/
	block ToFlatString()
		str = "{"
		first = true
		for member in members {
			if !first {
				str += ","
			else
				first = false
			}
			str += member.ToFlatString()
		}
		str += "}"
		return str	
		
	/*
	
	*/
	block ToString(string params...)
		paramsLen = LengthOf(params)
		if paramsLen == 0 {
		elif paramsLen == 1
			iindent = params[0]
			if IsWhiteSpace(iindent) {
				indent += iindent
			}
		else
			Throw(self, "Calling block with extra parameters")
		}
		if LengthOf(members) == 0 { return "{}" }
		str = "{" + Ascii(10) 
		indent += "    "
		first = true
		for member in members {
			if !first {
				str += ","
				str += Ascii(10)
			else
				first = false
			}
			str += indent + member.ToString(indent)
		}
		str += Ascii(10) + StrLeft(indent, LengthOf(indent) - 5)  + "}"
		return str
		
	/*
	
	*/
	block Size()
		return LengthOf(members)

	/*

	*/
	block Operator(string op, object value) 
		switch op {
			case "lengthOf"
				return LengthOf(members)
			case "=="
				return Equals(value)
			case "!="
				return !Equals(value)
			case "[]"
				return Get(value)
		}
		return super.Operator(op, value)
		
	/*
	
	*/
	block Equals(object obj)
		if !IsObject(obj) or !__instance_of(obj,"JsonObject") {
			return false
		}
		if Size() != obj.Size() {
			return false
		}
		for a = 0 to Size()-1 {
			if (!Get(a).Equals(obj.Get(a))) {
				return false
			}
		}
		return true
		
	/*
	
	*/
	block ToList()
		lst = members
		return lst
		
	/*
	
	*/
	block AsArray()
		return members
		
	/*
	
	*/
	block IsJsonObject()
		return true
		
	/*
	
	*/
	block IsJsonArray()
		return false
		
	/*
	
	*/
	block Join(/*JsonObject|JsonMember*/object params...)
		paramsLen = LengthOf(params)
		union = false
		newMembers = null
		if paramsLen == 1 {
			newMembers = params[0]
			if !InstanceOf(newMembers, "JsonObject") and !InstanceOf(newMembers, "JsonMember") {
				Throw(self, "Invalid parameters, Expected (JsonObject|JsonMember), Found (" + ObjectType(newMembers) + ")")
			}
		elif paramsLen == 2
			newMembers = params[0]
			union = BooleanValue(params[1])
			if (!InstanceOf(newMembers, "JsonObject") and !InstanceOf(newMembers, "JsonMember")) {
				Throw(self, "Invalid parameters, Expected (JsonObject|JsonMember, boolean), Found (" + ObjectType(newMembers) + ")")
			}
		else
			Throw(self, "Calling block with invalid lengthOf parameters")
		}
		if InstanceOf(newMembers, "JsonObject") {
			for member in newMembers {
				if union {
					if Contains(member.Name) {
						Set(member.Name, member)
					else 
						Add(member)
					}
				else
					Add(member)
				}
			}
		else
			if union {
				if Contains(newMembers.Name) {
					Set(newMembers.Name, newMembers)
				else 
					Add(newMembers)
				}
			else
				Add(newMembers)
			}
		}
		
	/*
	
	*/
	private
	
		/*
		
		*/
		string indent = ""
	
	
	
	
		