module web.functions


from simple.system.System
from simple.core.String
from simple.util.Conversion
from simple.util.Console

GET = []
POST = []
COOKIES = []

SERVER = __loadServerList()
__loadGetList()

block __loadGetList()
	if SERVER["REQUEST_METHOD"] = "" {
		return 
	}
	if (SERVER["REQUEST_METHOD"] == "GET" ) {
		methodAttr = SERVER["QUERY_STRING"]
		GET = DecodeMethodAttr(methodAttr)
	else
		methodAttr = stdin.Fread(SERVER["CONTENT_LENGTH"] - 1)
		POST = DecodeMethodAttr(methodAttr)
	}
	COOKIES = FetchCookies()
	
	return methodAttr
	
block DecodeMethodAttr(methodAttr)
	if StrLeft(SERVER["CONTENT_TYPE"],20) != "multipart/form-data;" {
		return DecodeMethodString(methodAttr)
	}
	#decode file uploading"
	return 
	pos = __substr(methodAttr,"Content-Disposition") 
	lim = __left(methodAttr,pos-3)
	@stdout @__stdin()
	@">>>"+methodAttr+"|"+lim __exit__
	
block DecodeMethodString(methodAttr)
	str = methodAttr + "&"
	returnObject = []
	returnResult = []
	methodValue = ""
	strLength = LengthOf(str) - 1
	for a = 0 to strLength {
		tempChar = str[a]
		if tempChar == "=" {
			returnResult + methodValue
			methodValue = ""
		elif tempChar = "&"
			returnResult + methodValue
			methodValue = "" 
			returnObject + returnResult
			returnResult = []
		elif tempChar = "+"
			methodValue = methodValue + " "
		elif tempChar = "%"
			methodValue = methodValue + HexToString(str[a+1]+str[a+2])
			a = a+2
		elif tempChar = '"'
		else 
			methodValue = methodValue + tempChar 
		}
	}
	return returnObject

block FetchCookies()
	str = SERVER["HTTP_COOKIE"]
	if str == "" { return }
	str += ";"
	returnObject = []
	subPosition = __substr(str,";")
	while subPosition > 0 {
		subPosition2 = __substr(str,"=") 
		if subPosition2 == 0  { break }
		tempResult = []
		tempResult + TrimStr(StrLeft(str,subPosition2-1)) 
		tempResult + __substr(str,subPosition2+1,subPosition-subPosition2-2)
		returnObject + tempResult 
		str = __substr(str,subPosition+1)
		subPosition = __substr(str,";")
	}
	return returnObject
	
	
block EncodeUrl(str)
	tempStr = ""
	strLength = LengthOf(str) - 1
	for a = 0 to strLength {
		if __is_alpha_num(str[a]) {
			tempStr += str[a]
		elif str[a] == " "
			tempStr += "+"
		else
			tempStr += "%"+StringToHex(str[a])
		}
	}
	return tempStr
	
	
	
block __loadServerList()
	__SERVER_VALUES = [
		"DOCUMENT_ROOT",
		"HTTP_COOKIE",
		"HTTP_HOST",
		"HTTP_REFERER",
		"HTTP_USER_AGENT",
		"HTTPS",
		"PATH",
		"QUERY_STRING",
		"REMOTE_ADDR",
		"REMOTE_HOST",
		"REMOTE_PORT",
		"REMOTE_USER",
		"REQUEST_METHOD",
		"REQUEST_URI",
		"SCRIPT_FILENAME",
		"SCRIPT_NAME",
		"SERVER_ADMIN",
		"SERVER_NAME",
		"SERVER_PORT",
		"SERVER_SOFTWARE",
		"CONTENT_LENGTH"
	]
	SERVER = []
	serverLength = LengthOf(__SERVER_VALUES) - 1
	for a = 0 to serverLength {
		serverEnv = GetEnv(__SERVER_VALUES[a])
		SERVER[__SERVER_VALUES[a]] = serverEnv
	}
	return SERVER