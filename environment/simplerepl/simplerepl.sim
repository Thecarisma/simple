
/* 
	Copyright (c) 2018 Azeez Adewale <azeezadewale98@gmail.com> 
	MIT License Copyright (c) 2018 SIMPLE 

*/

from simple.util.Console
from simple.system.System
from simple.core.String

multiLine = false
simpleReplTitle = "simple "+SimpleVersion() + "\n"

ClearConsole()
stdout.Printfc(ConsoleColor.None, simpleReplTitle)
while true {
	stdout.Printfc(ConsoleColor.Cyan, "simple$ ") 
	read code
	if code == "-\" {
		multiLine = true
		code = ""
	}
	while StrEndsWith(code, "\") or multiLine {
		read newCode
		if newCode == "-/" {
			multiLine = false
			break
		}
		if multiLine {
			code += nl
		else
			code[LengthOf(code)-1] = " "
		}
		code += newCode
	}
	if code == "__exit__" or code == "bye" or code == ":q" {
		break
	}
	try {
		ExecuteCode(code)
	catch
		stderr.Printfc(ConsoleColor.None, __err__+"\n")
	}
} 
