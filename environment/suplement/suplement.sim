/*

*/
/*
	Update simple-lang over JSON api (supplementary). Only update
	minor such as module, environments, dynamic modules and 
	documentation. It does not update the simple executable and 
	libraries (dll, so, dylib, a) and the includes files. 
	Update can happen in background except for the dynamic 
	modules since update can fail if they are in use
*/

from parser.json.JsonParser
from archive.zip.Zip
from simple.net.Download
from simple.util.Console
from simple.system.SimpleX
import simple.core
import simple.io


block main()
	if !__file_exists("./suplement.json") {
		stdout.println("The updater file 'suplement.json' file does not exist. exiting")
		exit(1)
	}
	localJson = new JsonParser(__read_file(simpleResDir()+"/suplement.json"))
	localJson = localJson.parse()
	installDir = localJson["InstallationDir"].Value
	
	if installDir == "$EXEC_DIR" {
		installDir = simpleDir()
	}
	updateFile = new Download(localJson["UpdateJsonAddress"].Value,getTmpDirectory()+"/update_file.json") {
		if !fetch() {
			stdout.println("Update failed")
			exit(2)
		}
	}
	updateJson = new JsonParser(updateFile.readAllString())
	updateJson = updateJson.parse()
	
	if !compareVersion(localJson["Version"].Value, updateJson["Version"].Value) {
		stdout.println("Your local version is up to date")
		return		
	}
	stdout.printf("preparing to update program...")
	updates = updateJson.get("Updates").Value
	platform = "Windows"
	if isLinux() {
		platform = "Linux"
	}
	for update in updates {
		stdout.printf("\r%s\rupdating %s...", strCopy(" ", 40), update.get("Name").Value)
		if update.get("Type").Value == "zip" {
			name = update.get("Name").Value
			path = update.get(platform).Value.get("Installation Path").Value
			stdout.printf("\r%s\rupdating %s, downloading...", strCopy(" ", 40), name)
			remoteZip = new Download(update.get(platform).Value.get("Update Url").Value, getTmpDirectory()+name+".zip") {
				if !fetch() {
					stdout.printf("\r%s\rupdating %s, downloading...(failed)", strCopy(" ", 40), name)
					break
					exit(3)
				}
				zip = new Zip(getTmpDirectory()+name+".zip", "r") {
					extract(installDir + "/"+path )
				}
			}
			stdout.print("(done)")
			
		elif update.get("Type").Value == "link" 
			stdout.print("(done)")
			system('start "" "'+update.get(platform).Value.get("Update Url").Value+'" ')
			
		}
	}
	localJson["Version"].Value = updateJson["Version"].Value
	__write_file(simpleResDir()+"/suplement.json", localJson.toString())
	
block compareVersion(localVersion, updateVersion)
	if lengthOf(localVersion) < lengthOf(updateVersion) {
		return true
	}
	localV = strSplit(localVersion,'.')
	localVLen = lengthOf(localV) - 1
	updateV = strSplit(updateVersion,'.')
	updateVLen = lengthOf(updateV) - 1
	#stdout.printf("%o, %o\n", localV, updateV)
	if localVLen < updateVLen {
		return true
	}
	for a = 0 to localVLen {
		if localV[a] < updateV[a] {
			return true
		}
	}	
	return false 
	
block getTmpDirectory()
	tempDirectory = getTempDirectory()
	tDirectory = new Directory(tempDirectory+"/.simple_env/updater/")
	if !tDirectory.exists() {
		tDirectory.create()
	}
	return tDirectory.AbsolutePath + SystemSlash()
	
	